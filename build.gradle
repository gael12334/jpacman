plugins {
    id 'application'
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id "com.github.spotbugs" version "6.4.8"
    id 'org.sonarqube' version '7.2.2.6593'
}

sourceSets {
    defaultTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/default-test/java')
        }
        resources.srcDir file('src/default-test/resources')
    }
}

configurations {
    defaultTestCompile.extendsFrom testCompile
    defaultTestImplementation.extendsFrom testImplementation
    defaultTestRuntime.extendsFrom testRuntime
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.guava:guava:32.0.1-android"
    compileOnly "com.github.spotbugs:spotbugs-annotations:$spotbugsAnnotationsVersion"
    //testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    // Source: https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.1.0-M1'
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    // Source: https://mvnrepository.com/artifact/org.assertj/assertj-core
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
}

tasks.register('defaultTest', Test) {
    testClassesDirs = sourceSets.defaultTest.output.classesDirs
    classpath = sourceSets.defaultTest.runtimeClasspath
}
test.dependsOn defaultTest

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

defaultTest {
    useJUnitPlatform()
}

jacoco {
    toolVersion = jacocoVersion
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

jacocoTestReport {
    executionData tasks.withType(Test)
    reports {
        xml.required = true
        layout.buildDirectory.dir('jacocoxml')
        csv.required = true
        layout.buildDirectory.dir('jacococsv')
    }
}

checkstyle {
    configFile file("checkstyle.xml")
    ignoreFailures = true
}

pmd {
    ruleSetFiles "pmd-rules.xml"
    ruleSets = []
}

tasks.spotbugsMain {
    reports.create("html") {
        required = true;
    }
    reports.create("xml") {
        required = true;
    }
}

tasks.register('staticAnalysis', GradleBuild) {
    tasks = [
            'clean',
            'checkstyleMain',
            'checkstyleTest',
            'checkstyleDefaultTest',
            'pmdMain',
            'pmdTest',
            'pmdDefaultTest',
            'spotbugsMain',
            'spotbugsTest',
            'spotbugsDefaultTest',
    ]
}

mainClassName = 'nl.tudelft.jpacman.Launcher'
